################################################################################
# Copyright (c) 2012 Los Alamos National Security, LLC
# All rights reserved.
################################################################################

#------------------------------------------------------------------------------#
# Initialize autoconf
#------------------------------------------------------------------------------#

AC_INIT(ska, VERSION_TAG, bergen@lanl.gov)
AC_CONFIG_AUX_DIR(config)

CPPFLAGS="$CPPFLAGS -DSKA_VERSION=VERSION_TAG"

#------------------------------------------------------------------------------#
# Initialize automake
#------------------------------------------------------------------------------#

AM_INIT_AUTOMAKE

#------------------------------------------------------------------------------#
# Machine file specification
#------------------------------------------------------------------------------#

CONFIG_WITH_MACHINE("$srcdir/machine")

#------------------------------------------------------------------------------#
# Configuration targets
#------------------------------------------------------------------------------#

AC_CONFIG_FILES([Makefile SANDBOX_SUBDIR \
	lib/Makefile \
	bin/Makefile \
	bin/skair \
	viewer/Makefile
])

SANDBOX_SUBST

#------------------------------------------------------------------------------#
# Program options
#------------------------------------------------------------------------------#

AC_PROG_CXX
AC_PROG_LIBTOOL
CONFIG_PROG_LLVM_CONFIG
CONFIG_PROG_MOC
CONFIG_PROG_RCC

#------------------------------------------------------------------------------#
# Enable options
#------------------------------------------------------------------------------#

CONFIG_GENERIC_ENABLE(debug, DEBUG)
CONFIG_GENERIC_ENABLE(color, COLOR)

#------------------------------------------------------------------------------#
# With options
#------------------------------------------------------------------------------#

CONFIG_GENERIC_WITH(dragonegg, DRAGONEGG)

#------------------------------------------------------------------------------#
# LLVM
#------------------------------------------------------------------------------#

if test "$HAS_LLVM_CONFIG" = "yes" ; then
	LLVM_DEPENDS=`llvm-config --libs`" -lpthread -ldl"
fi

CPPFLAGS="$CPPFLAGS -D__STDC_LIMIT_MACROS -D__STDC_CONSTANT_MACROS"

CONFIG_GENERIC_LIB(llvm, LLVM, [llvm::Pass::createPass((llvm::AnalysisID) 0)],
	C++, [], [$LLVM_DEPENDS])

EXTRA_CPPFLAGS="$EXTRA_CPPFLAGS $LLVM_CPPFLAGS"
EXTRA_LDFLAGS="$EXTRA_LDFLAGS $LLVM_LDFLAGS"
EXTRA_LIBS="$EXTRA_LIBS $LLVM_LIBS"

#------------------------------------------------------------------------------#
# Xerces
#------------------------------------------------------------------------------#

CONFIG_GENERIC_LIB(xerces-c, XERCES,
   [XERCES_CPP_NAMESPACE::XMLString::isAlpha((XMLCh const) 0)], C++)

EXTRA_CPPFLAGS="$EXTRA_CPPFLAGS $XERCES_CPPFLAGS"
EXTRA_LDFLAGS="$EXTRA_LDFLAGS $XERCES_LDFLAGS"
EXTRA_LIBS="$EXTRA_LIBS $XERCES_LIBS"

#------------------------------------------------------------------------------#
# Graphviz
#------------------------------------------------------------------------------#

CONFIG_GENERIC_LIB(graphviz, GRAPHVIZ, [gvContext()], C)

EXTRA_CPPFLAGS="$EXTRA_CPPFLAGS $GRAPHVIZ_CPPFLAGS"
EXTRA_LDFLAGS="$EXTRA_LDFLAGS $GRAPHVIZ_LDFLAGS"
EXTRA_LIBS="$EXTRA_LIBS $GRAPHVIZ_LIBS"

#------------------------------------------------------------------------------#
# Qt
#------------------------------------------------------------------------------#

CONFIG_GENERIC_LIB(qt, QT, [QApplication::beep()], C++)

VIEWER_CPPFLAGS="$QT_CPPFLAGS"
VIEWER_LDFLAGS="$QT_LDFLAGS"
VIEWER_LIBS="$QT_LIBS"

#------------------------------------------------------------------------------#
# Qwt
#------------------------------------------------------------------------------#

CONFIG_GENERIC_LIB(qwt, QWT, [QwtPlot((QWidget *) 0)], C++,
	[$QT_CPPFLAGS/Qt])

VIEWER_CPPFLAGS="$VIEWER_CPPFLAGS $QWT_CPPFLAGS"
VIEWER_LDFLAGS="$VIEWER_LDFLAGS $QWT_LDFLAGS"
VIEWER_LIBS="$VIEWER_LIBS $QWT_LIBS"

#------------------------------------------------------------------------------#
# Set extras
#------------------------------------------------------------------------------#

AC_SUBST(EXTRA_CPPFLAGS)
AC_SUBST(EXTRA_LDFLAGS)
AC_SUBST(EXTRA_LIBS)

AC_SUBST(VIEWER_CPPFLAGS)
AC_SUBST(VIEWER_LDFLAGS)
AC_SUBST(VIEWER_LIBS)

#------------------------------------------------------------------------------#
# Handle Doxygen
#------------------------------------------------------------------------------#

#if test "$HAS_DOXYGEN" = "yes" ; then
#	AC_SUBST(DOC, [doc])
#	AC_CONFIG_COMMANDS([doc], [make doc])
#fi

AC_CONFIG_COMMANDS([links], [make links])
AC_CONFIG_COMMANDS([skair], [chmod +x bin/skair])

#------------------------------------------------------------------------------#
# Output
#------------------------------------------------------------------------------#

AC_OUTPUT
